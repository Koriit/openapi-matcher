/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "com.github.ben-manes.versions" version "0.41.0"
    id "com.adarshr.test-logger" version "3.1.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    mavenCentral()
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // YAML
    implementation "org.yaml:snakeyaml:1.30"

    // LOGGING
    implementation "com.koriit.kotlin:slf4j-utils:0.8.0"

    // TESTING
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
    testImplementation "com.koriit.kotlin:slf4j-utils-logback:0.4.0"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+((-r)|(release)|(final)|(ga))?$/
    return !(version.toLowerCase() ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.43.2"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.7"
}

if (!hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java

            pom {
                name = "OpenAPI Matcher"
                description = "OpenAPI matcher for kotlin, allows to compare 2 specifications against each other"
                url = "https://github.com/Koriit/openapi-matcher"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/openapi-matcher/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = 'koriit'
                        name = 'Aleksander Stelmaczonek'
                        email = 'al.stelmaczonek@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Koriit/openapi-matcher.git'
                    developerConnection = 'scm:git:ssh://github.com/Koriit/openapi-matcher.git'
                    url = 'https://github.com/Koriit/openapi-matcher'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = findProperty("sonatype.user") ?: System.getenv("SONATYPE_USERNAME")
                password = findProperty("sonatype.password")?: System.getenv("SONATYPE_PASSWORD")
            }

            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    def signingKey = findProperty("signing.key") ?: System.getenv("PGP_SIGNING_KEY")
    def signingPassword = findProperty("signing.password") ?: System.getenv("PGP_SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
