/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
    id "com.jfrog.bintray" version "1.8.5"
    id "java-library"
    id "maven-publish"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "9.4.1"
    id "io.gitlab.arturbosch.detekt" version "1.14.2"
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.adarshr.test-logger" version "2.1.1"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    jcenter()
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // YAML
    implementation "org.yaml:snakeyaml:1.27"

    // LOGGING
    implementation "koriit.kotlin:slf4j-utils:0.7.1"

    // TESTING
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    testImplementation "koriit.kotlin:slf4j-utils-logback:0.3.0"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+((-r)|(release)|(final)|(ga))?$/
    return !(version.toLowerCase() ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.36.0"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

if (!hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "OpenAPI Matcher"
                description = "OpenAPI matcher for kotlin, allows to compare 2 specifications against each other"
                url = "https://github.com/Koriit/openapi-matcher"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/openapi-matcher/blob/master/LICENSE"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ["bintrayMaven"]

    pkg {
        repo = "kotlin"
        name = "openapi-matcher"
        version {
            name = project.version
            released = new Date()
        }
    }
}

///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
